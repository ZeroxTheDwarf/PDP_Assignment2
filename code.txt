-- Load the data from the orders.csv file.
ordersCSV = LOAD '/user/maria_dev/diplomacy/orders.csv'
	USING PigStorage(',')
	AS
		(game_id:chararray,
		unit_id:chararray,
		unit_order:chararray,
		location:chararray,
		target:chararray,
		target_dest:chararray,
		success:chararray,
		reason:chararray,
		turn_num:chararray);

--Remove the "" marks from the fields for better data usability/readability.
data_clean = FOREACH ordersCSV GENERATE
            REPLACE($0, '"', '') AS (game_id:chararray),
            REPLACE($1, '"', '') AS (unit_id:chararray),
            REPLACE($2, '"', '') AS (unit_order:chararray),
            REPLACE($3, '"', '') AS (location:chararray),
            REPLACE($4, '"', '') AS (target:chararray),
            REPLACE($5, '"', '') AS (target_dest:chararray),
            REPLACE($6, '"', '') AS (success:chararray),
            REPLACE($7, '"', '') AS (reason:chararray),
            REPLACE($8, '"', '') AS (turn_num:chararray);

-- Filter out all data records where the target of the action is not Holland.
data_filtered = FILTER data_clean BY target=='Holland';

-- Group the data so we can count the amount of actions towards Holland by different locations.
data_grouped = GROUP data_filtered BY (location, target);

-- Count the amount of tuples attached to each location.
data_counted = FOREACH data_grouped GENERATE FLATTEN(group) as (location, target), COUNT($1);

-- Order the countries by alphabetical order.
data_ordered = ORDER data_counted BY location;

-- Dump the data to confirm that the script works correctly.
DUMP data_ordered;
